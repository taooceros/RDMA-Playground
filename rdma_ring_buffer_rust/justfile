set dotenv-load

server_ip := env_var('SERVER_IP')

port := env_var('SERVER_PORT')
duration := env_var('DURATION')

message_size := env_var_or_default('MESSAGE_SIZE', '64')


adapter_executable := "rdma_adapter"
host_executable := "rdma_host"

alias b := build
alias s := server
alias c := client
alias k := kill

alias ch := client_host
alias ca := client_adapter

alias sh := server_host
alias sa := server_adapter

alias gc := git-commit
alias qs := quick-server

configuration := "release"

git_update:
	git pull

build: git_update
	cargo build {{ if configuration == "release" {"--release"} else {""} }} --all

server: build kill
	@just server_adapter > adapter.log &
	@just server_host > host.log &

server_adapter:
	./target/{{configuration}}/{{adapter_executable}} --port {{port}} -g 1 --message-size {{message_size}}

server_host:
	./target/{{configuration}}/{{host_executable}} server --batch-size {{message_size}} --duration {{duration}} 

client_adapter:
	./target/{{configuration}}/{{adapter_executable}} -s {{server_ip}} --port {{port}} -g 1 --message-size {{message_size}}

client_host:
	./target/{{configuration}}/{{host_executable}} client --batch-size {{message_size}} --duration {{duration}} 

client: build kill
	@just client_adapter > adapter.log &
	@just client_host > host.log &

git-commit message="update":
	git add .
	git commit -am "{{message}}" || true
	git push

quick-server message="update": (git-commit message) && server


kill:
	rm sync || true
	killall {{adapter_executable}} || true
	killall {{host_executable}} || true
