server_ip := "192.168.2.1"
port := "12873"
duration := "1"

adapter_executable := "rdma_adapter"
host_executable := "rdma_host"

alias s := server
alias c := client

alias gc := git-commit
alias qs := quick-server

configuration := "debug"

git_update:
	git pull

build: git_update
	cargo build {{ if configuration == "release" {"--release"} else {""} }}

server message_size="64": build kill
	./target/{{configuration}}/{{adapter_executable}} --port {{port}} -g 1 --message-size {{message_size}} > adapter.log &
	./target/{{configuration}}/{{host_executable}} server --duration {{duration}} > host.log &

server_adapter: build
	./target/{{configuration}}/{{adapter_executable}} --port {{port}} -g 1

server_host: build
	./target/{{configuration}}/{{host_executable}} server

client_adapter: build
	./target/{{configuration}}/{{adapter_executable}} -s {{server_ip}} --port {{port}} -g 1

client_host: build
	./target/{{configuration}}/{{host_executable}} client

client message_size="64": build kill
	./target/{{configuration}}/{{adapter_executable}} -s {{server_ip}} --port {{port}} -g 1 --message-size {{message_size}} > adapter.log &
	./target/{{configuration}}/{{host_executable}} client --duration {{duration}} > host.log &

git-commit message="update":
	git add .
	git commit -am "{{message}}"
	git push

quick-server message="update": (git-commit message) && server

kill:
	rm sync || true
	killall {{adapter_executable}} || true
	killall {{host_executable}} || true
