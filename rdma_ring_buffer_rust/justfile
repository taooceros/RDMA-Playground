server_ip := "192.168.2.1"
port := "12873"

adapter_executable := "rdma_adapter"
host_executable := "rdma_host"

alias s := server
alias c := client

alias gc := git-commit
alias qs := quick-server

git_update:
	git pull

build: git_update
	cargo build

server: build kill
	./target/debug/{{adapter_executable}} --port {{port}} -g 1 > adapter.log &
	./target/debug/{{host_executable}} --port {{port}} -g 1 > host.log &

server_adapter: build
	./target/debug/{{adapter_executable}} --port {{port}} -g 1

server_host: build
	./target/debug/{{host_executable}} --port {{port}} -g 1

client_adapter: build
	./target/debug/{{adapter_executable}} -s {{server_ip}} --port {{port}} -g 1

client_host: build
	./target/debug/{{host_executable}} -s {{server_ip}} --port {{port}} -g 1

client: build kill
	./target/debug/{{adapter_executable}} -s {{server_ip}} --port {{port}} -g 1 > adapter.log &
	./target/debug/{{host_executable}} -s {{server_ip}} --port {{port}} -g 1 &

git-commit message="update":
	git add .
	git commit -am "{{message}}"
	git push

quick-server message="update": (git-commit message) && server

kill:
	rm sync || true
	killall {{adapter_executable}} || true
	killall {{host_executable}} || true
